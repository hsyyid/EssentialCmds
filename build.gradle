plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'maven'
    id "com.github.hierynomus.license" version "0.12.1"
    id 'net.ellune.blossom' version '1.0.1'
}

group = "io.github.hsyyid"
archivesBaseName = "EssentialCmds"
version = '6.5'

apply plugin: 'net.ellune.blossom'

sourceCompatibility = 1.8
targetCompatibility = 1.8

defaultTasks 'licenseFormat'

jar {
    manifest {
        attributes 'Implementation-Title': 'EssentialCmds',
                   'Implementation-Version': version
    }
}

repositories {
    jcenter()
    maven {
        name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile "org.spongepowered:spongeapi:3.0.0"
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

license {
    //the project name to use
    ext.name = project.name
    ext.owner = project.owner
    ext.inceptionYear = project.inceptionYear
    ext.currentYear = project.currentYear

    //for usage of these values see this: https://github.com/hierynomus/license-gradle-plugin#variable-substitution

    //files to exclude from formatting (anything that is a .info file and assets in this example)
    exclude "**/*.info"
    exclude "assets/**"

    //the file that contains the header
    header file('HEADER.txt')

    //the source sets to apply the license to
    //this should be enough for you, it covers all the projects source sets
    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    //what style your comment should be
    mapping {
        //in this case it's the following: /* and then continuing with stars.
        java = 'SLASHSTAR_STYLE'
    }
}

def getGitHash() {
    def process = 'git rev-parse --short HEAD'.execute()
    process.waitFor()
    return '-' + (process.exitValue() ? 'unknown' : process.text.trim())
}

blossom {
    replaceToken '@project.version@', project.version + getGitHash(), 'src/main/java/io/github/hsyyid/essentialcmds/PluginInfo.java'
}